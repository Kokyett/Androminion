plugins {
    id 'com.android.application'
}

android {
    namespace 'com.mehtank.androminion'
    compileSdkVersion 36

    String versionNameEnv = System.getenv("VERSION_NAME") ?: "develop"
    Integer versionCodeEnv = Integer.parseInt(new Date().format('yyMMdd'))

    defaultConfig {
        applicationId "com.mehtank.androminion"
        minSdk 21
        targetSdk 36
        versionCode versionCodeEnv
        versionName versionNameEnv
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    signingConfigs {
        release {
            storeFile file("keystore.jks")
            storePassword System.getenv("SIGNING_STORE_PASSWORD")
            keyAlias System.getenv("SIGNING_KEY_ALIAS")
            keyPassword System.getenv("SIGNING_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        applicationVariants.all { variant ->
            variant.outputs.each { output->
                output.outputFileName = "Androminion_${variant.versionName}.apk"
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    implementation project(path: ':vdom')
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.viewpager2:viewpager2:1.1.0'
    implementation 'com.google.android.material:material:1.12.0'
}
